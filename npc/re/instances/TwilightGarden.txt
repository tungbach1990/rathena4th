//===== rAthena Script =======================================
//= Twilight Garden
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.2 Twilight Garden
//= Episode 17.2 Hey Sweety
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//============================================================

1@bamn,1,1,0	script	#twilight_garden_control	-1,{
	end;

OnInstanceInit:
	'twilight_story = 0;
	'map$ = instance_mapname("1@bamn");
	disablenpc instance_npcname("Almond#172_tgd_0");
	disablenpc instance_npcname("Almond#172_hst");
	disablenpc instance_npcname("Sweety#172_hst");
	disablenpc instance_npcname("#tgd_navi_0");
	disablenpc instance_npcname("#tgd_navi_1");
	for (.@i = 0; .@i < 5; .@i++) {
		disablenpc instance_npcname("Heart Hunter#172_tgd_" + .@i);
		disablenpc instance_npcname("Est#172_tgd_" + .@i);
	}
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Rebellion#172_tgdm_1-" + .@i);
		disablenpc instance_npcname("Rebellion#172_tgdm_2-" + .@i);
	}
	for (.@i = 0; .@i < 3; .@i++) {
		disablenpc instance_npcname("Heart Hunter#172_tgdm_1-" + .@i);
		disablenpc instance_npcname("Heart Hunter#172_mob_" + .@i);
		disablenpc instance_npcname("Intruder#172_tgd_" + .@i);
	}
	disablenpc instance_npcname("#tgd_bamn_to_bamnq");
	disablenpc instance_npcname("#tgd_bamn_to_bamnq2");
	disablenpc instance_npcname("#tgd_bamq_exit");
	disablenpc instance_npcname("#tgd_bamq_exit2");
	end;

OnStory01:
	'twilight_story = 2;
	for (.@i = 0; .@i < 5; .@i++)
		enablenpc instance_npcname("Heart Hunter#172_tgd_" + .@i);
	end;

OnStory02:
	for (.@i = 0; .@i < 5; .@i++)
		disablenpc instance_npcname("Heart Hunter#172_tgd_" + .@i);
	donpcevent instance_npcname("#twilight_story_mob") + "::OnSpawn00";
	end;

OnStory03:
	'twilight_story = 4;
	hideonnpc();
	disablenpc instance_npcname(strnpcinfo(0));
	donpcevent instance_npcname("#twilight_story_mob") + "::OnSpawn01";
	end;

OnStory04:
	'twilight_story = 15;
	for (.@i = 0; .@i < 4; .@i++)
		enablenpc instance_npcname("Rebellion#172_tgdm_1-" + .@i);
	for (.@i = 0; .@i < 3; .@i++)
		enablenpc instance_npcname("Heart Hunter#172_tgdm_1-" + .@i);
	end;

OnStory05:
	'twilight_story = 18;
	enablenpc instance_npcname("Est#172_tgd_3");
	for (.@i = 0; .@i < 3; .@i++) {
		enablenpc instance_npcname("Rebellion#172_tgdm_2-" + .@i);
		enablenpc instance_npcname("Heart Hunter#172_mob_" + .@i);
	}
	enablenpc instance_npcname("Rebellion#172_tgdm_2-3");
	end;

OnStory06:
	'twilight_story = 21;
	enablenpc instance_npcname("#tgd_bamn_to_bamnq");
	enablenpc instance_npcname("Intruder#172_tgd_2");
	enablenpc instance_npcname("#tgd_navi_0");
	enablenpc instance_npcname("#tgd_navi_1");
	end;
}

1@bamn,100,320,0	script	#172_tgd_ev	HIDDEN_WARP_NPC,2,2,{
	end;

OnTouch:
	if (!is_party_leader())
		end;
	disablenpc();
	disablenpc instance_npcname("#172_hst_ev");
	enablenpc instance_npcname("Est#172_tgd_0");
	end;
}

1@bamq,16,39,0	script	#172_hst_ev	HIDDEN_WARP_NPC,2,2,{
OnTouch:
	if (!is_party_leader())
		end;
	disablenpc();
	enablenpc instance_npcname("Almond#172_hst");
	enablenpc instance_npcname("Sweety#172_hst");
	enablenpc instance_npcname("#tgd_bamn_to_bamnq2");
	enablenpc instance_npcname("#tgd_bamq_exit2");
	end;
}

1@bamn,96,318,5	script	Est#172_tgd_0	4_F_ESTLOVELOY,{
	if ('twilight_story == 0 && is_party_leader()) {
		'twilight_story = 1;
		npctalk "Est : It's dark and quiet. If you want to execute the plan, now's the perfect time. Right?";
		sleep 2000;
		npctalk "Est : Elena said she'd join us later, but there's a good chance she won't able to join.";
		sleep 2000;
		npctalk "Est : Even so, there's no need to worry. The others are prepared and waiting for the ambush signal.";
		sleep 2000;
		npctalk "Est : All we have to do is walk around like a patrol and catch them off guard.";
		sleep 2000;
		npctalk "Est : Go along the road to the main building of the mansion. I'll lurk behind you.";
		sleep 2000;
		npctalk "Est : Don't look around too much, and pretend you're not vigilant, got it?";
		sleep 2000;
		npctalk "Est : Now, let's go.";
		donpcevent instance_npcname("#twilight_garden_control") + "::OnStory01";
		end;
	}
	if ('twilight_story == 2) {
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "I'll move quietly to the back of the building.";
		mes "The others are lurking around, waiting for the signal.";
		next;
		mes "[ Est ]";
		mes "So it'll look like you're patrolling alone. ";
		mes "First, head to the main building of the mansion. If we can't spot anything special there, we'll head to the next area.";
		close3;
	}
	if ('twilight_story == 3) {
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "They must have been waiting too.";
		mes "Let's do this more quietly.";
		close3;
	}
	end;
}

1@bamn,119,299,3	script	Heart Hunter#172_tgd_0	20681,3,3,{
	end;

OnTouch:
	if ('twilight_story == 2) {
		'twilight_story = 3;
		npctalk "Quietly surrender or return back to where you came from.";
		sleep 2000;
		npctalk "Well, that doesn't mean we'll let you go back.";
		sleep 1000;
		donpcevent instance_npcname("#twilight_garden_control") + "::OnStory02";
	}
	end;
}

1@bamn,119,305,3	duplicate(dummy_npc)	Heart Hunter#172_tgd_1	20681
1@bamn,119,302,3	duplicate(dummy_npc)	Heart Hunter#172_tgd_2	20681
1@bamn,119,296,3	duplicate(dummy_npc)	Heart Hunter#172_tgd_3	20681
1@bamn,119,293,3	duplicate(dummy_npc)	Heart Hunter#172_tgd_4	20681

1@bamn,119,299,5	script	Est#172_tgd_1	4_F_ESTLOVELOY,{
	if ('twilight_story == 3) {
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "I don't know if we should set a trap, or give you a chance.";
		mes "Activate the tracker that we got from Elyumina.";
		next;
		cutin "",255;
		unittalk getcharid(3),strcharinfo(0) + " : Tracker : According to the intuition of the great Elyumina... Too Doo Too Doo Too~";
		sleep2 2000;
		unittalk getcharid(3),strcharinfo(0) + " : Tracker : Over there!! Beep!";
		navigateto("1@bamn",206,273);
		sleep2 2000;
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "That's a loud thing.";
		mes "Will you move out first?";
		mes "I have a place to go for a second.";
		next;
		mes "[ Est ]";
		mes "I'll join you soon.";
		if ('twilight_story == 3)
			donpcevent instance_npcname("#twilight_garden_control") + "::OnStory03";
		close2;
		cutin "",255;
	}
	end;
}

1@bamn,202,274,5	script	Est#172_tgd_2	4_F_ESTLOVELOY,{
	if ('twilight_story == 5) {
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "I thought it might be better to throw in more bait, so I went to ask for the help of the automatic dolls.";
		mes "Also, Almond said she had to go out and check something.";
		next;
		cutin "ep172_beta",0;
		mes "[ Almond ]";
		mes "The repeater is dead again. That's why I have to fix it.";
		mes "The repeater is the favorite target of the intruders, so we can't just leave it broken, right?";
		next;
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "For that reason, I'd like to ask you to escort Almon here to check the repeater.";
		mes "I'll look around and find other routes.";
		next;
		cutin "ep172_beta",0;
		mes "[ Almond ]";
		mes "Let's go together whenever you're ready.";
		close2;
		cutin "",255;
		if ('twilight_story == 5)
			'twilight_story = 6;
		end;
	}
	if ('twilight_story == 6) {
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "Talk to Almond, when you're ready to move.";
		close3;
	}
	end;
}

1@bamn,199,275,5	script	Almond#172_tgd_0	20629,{
	if ('twilight_story == 5) {
		cutin "ep172_beta",2;
		mes "[ Almond ]";
		mes "Everyone was supposed to be in the mansion today.";
		mes "Something important came up. That's why I'm here with Est.";
		close3;
	}
	if ('twilight_story == 6) {
		'guide = getnpcid(0);
		'twilight_story = 7;
		npcspeed 200;
		npctalk "Then, let us go.";
		unitwalk 'guide,199,248,instance_npcname(strnpcinfo(0))+"::OnEvent00";
		enablenpc instance_npcname("Intruder#172_tgd_0");
		end;
	}
	if ('twilight_story == 8) {
		'twilight_story = 9;
		unittalk getcharid(3),strcharinfo(0) + " : Tracker : Pyo-o-o-o-o! There!!";
		setpcblock PCBLOCK_ALL,true;
		navigateto("1@bamn",242,206);
		sleep2 2000;
		npctalk "Ahahaha, it's noisy.";
		sleep2 2000;
		npctalk "I'm curious, should we go quickly?";
		sleep2 2000;
		npctalk "Guest, there is an intruder!";
		donpcevent instance_npcname("#twilight_story_mob") + "::OnSpawn02";
		setpcblock PCBLOCK_ALL,false;
		sleep 200;
		npcspeed 80;
		unitwalk 'guide,245,229,instance_npcname(strnpcinfo(0))+"::OnEvent02";
		end;
	}
	if ('twilight_story == 10 || 'twilight_story == 11) {
		npctalk "Be careful!";
		end;
	}
	if ('twilight_story == 12) {
		'twilight_story = 13;
		npcspeed 200;
		setpcblock PCBLOCK_ALL,true;
		npctalk "Let's go check the repeater.";
		sleep2 2000;
		setpcblock PCBLOCK_ALL,false;
		sleep2 1000;
		unitwalk 'guide,244,208,instance_npcname(strnpcinfo(0))+"::OnEvent04";
		end;
	}
	if ('twilight_story == 14) {
		setpcblock PCBLOCK_ALL,true;
		donpcevent instance_npcname("#twilight_garden_control") + "::OnStory04";
		unittalk getcharid(3),strcharinfo(0) + " : Tracker : Kakakakakakakil! The culprit is around! They're on my radar! It's that way!";
		navigateto("1@bamn",332,143);
		sleep2 2000;
		setpcblock PCBLOCK_ALL,false;
		sleep2 1000;
		npcspeed 80;
		npctalk "There's another intruder! No wonder I'm starting to get excited~!";
		unitwalk 'guide,332,169;
	}
	if ('twilight_story == 18) {
		npcspeed 200;
		'twilight_story = 19;
		npctalk "Are you all right, adventurer? Then, we can keep going.";
		unitwalk 'guide,332,143,instance_npcname(strnpcinfo(0))+"::OnEvent11";
	}
	if ('twilight_story == 20) {
		npctalk "Shouldn't you talk to Est? Looks like she've been waiting.";
		end;
	}
	end;

OnEvent00:
	npctalk "This way.";
	sleep 1000;
	unitwalk 'guide,225,248,instance_npcname(strnpcinfo(0))+"::OnFollow00";
	end;

OnFollow00:
	unitwalk 'guide,245,248,instance_npcname(strnpcinfo(0))+"::OnEvent01";
	end;

OnEvent01:
	npctalk "Let's go see the Repeater that is oftenly hacked.";
	sleep 2000;
	npctalk "Huh? What are you carrying right now? Take it out and show it to me.";
	'twilight_story = 8;
	end;

OnEvent02:
	npctalk "Adventurer, there's an intruder!";
	unitwalk 'guide,245,215,instance_npcname(strnpcinfo(0))+"::OnEvent03";
	end;

OnEvent03:
	'twilight_story = 10;
	end;

OnEvent04:
	npctalk "...checking the signal.";
	sleep 2000;
	npctalk "...hacking channel checked.";
	sleep 2000;
	npctalk "...recovering...";
	sleep 2000;
	npctalk "...recovering...84%";
	sleep 2000;
	npctalk "...recovery completed.";
	sleep 2000;
	npctalk "Shall we go to the repeater?";
	sleep 2000;
	unitwalk 'guide,245,204,instance_npcname(strnpcinfo(0))+"::OnEvent05";
	end;

OnEvent05:
	npctalk "Adventurer. Do you like Almonds? A customer who liked almonds named me. Does it suit me?";
	sleep 3000;
	unitwalk 'guide,265,204,instance_npcname(strnpcinfo(0))+"::OnEvent06";
	end;

OnEvent06:
	npctalk "That kid a while ago, I've seen him before.";
	unitwalk 'guide,285,204,instance_npcname(strnpcinfo(0))+"::OnEvent07";
	end;

OnEvent07:
	npctalk "But he always ran so well, It's the first time I've seen him properly~";
	unitwalk 'guide,300,204,instance_npcname(strnpcinfo(0))+"::OnEvent08";
	end;

OnEvent08:
	npctalk "The Pitaya's here is cute~ They say Almonds is delicious? Have you eaten almonds before?";
	sleep 2000;
	npctalk "This way.";
	unitwalk 'guide,300,190,instance_npcname(strnpcinfo(0))+"::OnEvent09";
	end;

OnEvent09:
	npctalk "It's been a while since I've been this comfortable~";
	unitwalk 'guide,327,189,instance_npcname(strnpcinfo(0))+"::OnEvent10";
	end;

OnEvent10:
	npctalk "Adventurer. Is that noisy thing flickering again? Let us check it for a second.";
	'twilight_story = 14;
	end;

OnEvent11:
	npctalk "...checking the signal.";
	sleep 2000;
	npctalk "...hacking channel checked.";
	sleep 2000;
	npctalk "...recovering...";
	sleep 2000;
	npctalk "...recovering...72%";
	sleep 2000;
	npctalk "...recovery completed.";
	sleep 2000;
	npctalk "Let's go to the next repeater~";
	sleep 2000;
	unitwalk 'guide,320,139,instance_npcname(strnpcinfo(0))+"::OnFollow12";
	end;

OnFollow12:
	unitwalk 'guide,300,139,instance_npcname(strnpcinfo(0))+"::OnEvent12";
	end;

OnEvent12:
	npctalk "It's the kid from before, right? He's an intruder. That's weird, kind of like almonds...";
	unitwalk 'guide,285,139,instance_npcname(strnpcinfo(0))+"::OnEvent13";
	end;

OnEvent13:
	npctalk "Shall we go faster? There's a sound coming from over there...";
	unitwalk 'guide,285,118,instance_npcname(strnpcinfo(0))+"::OnEvent14";
	end;

OnEvent14:
	npctalk "It's more quiet than I thought. Where did the intruders go?";
	unitwalk 'guide,285,98,instance_npcname(strnpcinfo(0))+"::OnEvent15";
	end;

OnEvent15:
	npctalk "This way.";
	unitwalk 'guide,261,98,instance_npcname(strnpcinfo(0))+"::OnEvent16";
	end;

OnEvent16:
	npcspeed 80;
	npctalk "It's Est!";
	'twilight_story = 20;
	unitwalk 'guide,261,87;
	end;
}

1@bamn,242,206,7	script	Intruder#172_tgd_0	4_EP17_SWEETY,{
	if ('twilight_story < 11) {
		npctalk "You're one step behind! You bunch of idiots!";
		end;
	}
	if ('twilight_story == 11) {
		npctalk "Hah! Do you think you can catch me?";
		sleep 2000;
		npctalk "Go suffer!";
		sleep 500;
		disablenpc();
		donpcevent instance_npcname("#twilight_story_mob") + "::OnSpawn03";
		end;
	}
	end;
}

1@bamn,331,161,3	script	Rebellion#172_tgdm_1-0	4_F_REBELLION,7,7,{
	end;

OnTouch:
	if ('twilight_story == 15) {
		'twilight_story = 16;
		npctalk "We'll take over this place, just watch!";
		sleep 1000;
		specialeffect EF_DESPERADO,AREA,instance_npcname("Heart Hunter#172_tgdm_1-2");
		sleep 1000;
		npctalk "Kakaka, Let's beat them down with a blast!",instance_npcname("Rebellion#172_tgdm_1-2");
		specialeffect EF_TRIPLEACTION,AREA,instance_npcname("Heart Hunter#172_tgdm_1-1");
		sleep 1000;
		specialeffect EF_DESPERADO,AREA,instance_npcname("Heart Hunter#172_tgdm_1-1");
		sleep 2000;
		npctalk "Thank you for showing yourselves! You bastards!",instance_npcname("Rebellion#172_tgdm_1-1");
		sleep 2000;
		specialeffect EF_DESPERADO,AREA,instance_npcname("Heart Hunter#172_tgdm_1-0");
		sleep 2000;
		npctalk "...",instance_npcname("Heart Hunter#172_tgdm_1-1");
		sleep 2000;
		npctalk "...(Nods)",instance_npcname("Heart Hunter#172_tgdm_1-2");
		sleep 1000;
		npctalk "...",instance_npcname("Heart Hunter#172_tgdm_1-0");
		sleep 1000;
		npctalk "What signal are you sending to each other?";
		sleep 1000;
		specialeffect EF_DESPERADO,AREA,instance_npcname("Heart Hunter#172_tgdm_1-1");
		for (.@i = 0; .@i < 4; .@i++)
			specialeffect EF_SPREADATTACK,AREA,instance_npcname("Rebellion#172_tgdm_1-" + .@i);
		sleep 1000;
		for (.@i = 0; .@i < 3; .@i++)
			disablenpc instance_npcname("Heart Hunter#172_tgdm_1-" + .@i);
		sleep 1000;
		npctalk "What? Did they ran?! Move out!";
		sleep 2000;
		for (.@i = 0; .@i < 4; .@i++)
			disablenpc instance_npcname("Rebellion#172_tgdm_1-" + .@i);
		enablenpc instance_npcname("Intruder#172_tgd_1");
		npctalk "What...?",instance_npcname("Intruder#172_tgd_1");
	}
	end;
}

1@bamn,331,155,1	duplicate(dummy_npc)	Rebellion#172_tgdm_1-1	4_M_REBELLION
1@bamn,322,155,7	duplicate(dummy_npc)	Rebellion#172_tgdm_1-2	4_F_REBELLION
1@bamn,322,161,3	duplicate(dummy_npc)	Rebellion#172_tgdm_1-3	4_M_REBELLION
1@bamn,328,158,1	duplicate(dummy_npc)	Heart Hunter#172_tgdm_1-0	20681
1@bamn,326,159,3	duplicate(dummy_npc)	Heart Hunter#172_tgdm_1-1	20681
1@bamn,324,158,7	duplicate(dummy_npc)	Heart Hunter#172_tgdm_1-2	20681

1@bamn,333,141,7	script	Intruder#172_tgd_1	4_EP17_SWEETY,{
	if ('twilight_story == 16) {
		'twilight_story = 17;
		npctalk "What, where did the other guys go?";
		sleep 2000;
		npctalk "Did you do it?";
		sleep 2000;
		npctalk "You're pretty good, aren't you?";
		sleep 2000;
		npctalk "Repeater : (... .. ..)",instance_npcname("Repeater#twilight_garden_0");
		npctalk "Oh, there's a message. Stay there for a while. Yes, Hello.";
		sleep 2000;
		npctalk "Repeater : (... … ... ….)",instance_npcname("Repeater#twilight_garden_0");
		sleep 2000;
		npctalk "Now? More time... Yes, no... Ah...";
		sleep 2000;
		npctalk "... ... so far... no?!... Okay...";
		sleep 2000;
		emotion ET_THINK;
		sleep 2000;
		npctalk "You! You're the reason I don't have time right now! I'll kill you the next time we meet!";
		sleep 2000;
		disablenpc();
		donpcevent instance_npcname("#twilight_garden_control") + "::OnStory05";
	}
	end;
}

1@bamn,334,143,5	duplicate(dummy_npc)	Repeater#twilight_garden_0	2_POSTBOX

1@bamn,257,84,3	script	Est#172_tgd_3	4_F_ESTLOVELOY,{
	if ('twilight_story == 20) {
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "I've heard the report. You found a guy who looked like the leader?";
		mes "I think the rest of the members are somewhat good too.";
		next;
		mes "[ Est ]";
		mes "What happened to the repeater?";
		next;
		cutin "ep172_beta",2;
		mes "[ Almond ]";
		mes "Were on the way to the other one.";
		mes "But it's quite far from here.";
		next;
		cutin "ep162_est01",2;
		mes "[ Est ]";
		mes "Hmm... Shall we do this?";
		mes "The guy you missed is a boy with purple hair, right?";
		mes "We're going to bait him out.";
		next;
		mes "[ Est ]";
		mes "I'll deal with the rest who's hiding in the garden.";
		mes "Ah. I'll also escort Almond.";
		next;
		mes "[ Est ]";
		mes "He probably went to the western part of the mansion, so head over there.";
		mes "You have Elyumina's tracker. It will activate, if you get near him.";
		donpcevent instance_npcname("#twilight_garden_control") + "::OnStory06";
		close2;
		cutin "",255;
		end;
	}
	if ('twilight_story == 21) {
		mes "[ Est ]";
		mes "He probably went to the western part of the mansion, so head over there.";
		mes "You have Elyumina's tracker, if you get near him, it will activate.";
		next;
		mes "[ Est ]";
		mes "Don't worry, I'll take care of Almond from here.";
		close3;
	}
	end;
}

1@bamn,258,80,1	script	Rebellion#172_tgdm_2-0	4_M_REBELLION,{
	npctalk "If there's an enemy causing commotion, we should attack them with more numbers.";
	end;
}

1@bamn,254,80,7	script	Rebellion#172_tgdm_2-1	4_F_REBELLION,{
	npctalk "Hehehe... It's finally time for revenge... Hehehe...";
	end;
}

1@bamn,250,80,7	script	Rebellion#172_tgdm_2-2	4_M_REBELLION,{
	npctalk "Leave the rest to us.";
	end;
}

1@bamn,202,84,3	script	Rebellion#172_tgdm_2-3	4_M_REBELLION2,{
	npctalk "You're looking for a boy with purple hair, right? He went towards the labs.";
	end;
}

1@bamn,67,173,0	script	#tgd_navi_0	HIDDEN_WARP_NPC,5,5,{
	end;

OnTouch:
	if ('twilight_story == 21) {
		unittalk getcharid(3),strcharinfo(0) + " : Tracker : Pyo-o-o-o-o! That way! Pyo-pyo~!";
		navigateto("1@bamn",33,224);
	}
	end;
}

1@bamn,121,204,0	duplicate(#tgd_navi_0)	#tgd_navi_1	HIDDEN_WARP_NPC,7,7

1@bamn,61,214,7	script	Heart Hunter#172_mob_0	20699,5,5,{
	end;

OnTouch:
	.@type = atoi(replacestr(strnpcinfo(2),"172_mob_",""));
	switch (.@type) {
		case 0:
			setarray .@xy,61,214,20699,64,214,20699,58,212,20699;
			break;

		case 1:
			setarray .@xy,78,204,20699,83,204,20699,76,200,20699;
			break;

		case 2:
			setarray .@xy,67,195,20699,71,197,20699,65,199,20699;
			break;
	}
	emotion ET_GO;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Heart Hunter Commander",.@xy[.@i+2],1;
	disablenpc();
	end;
}

1@bamn,78,204,5	duplicate(Heart Hunter#172_mob_0)	Heart Hunter#172_mob_1	20699,5,5
1@bamn,67,195,3	duplicate(Heart Hunter#172_mob_0)	Heart Hunter#172_mob_2	20699,5,5


1@bamq,125,39,3	script	Intruder#172_tgd_2	4_EP17_SWEETY,{
	if ('twilight_story == 21) {
		setpcblock PCBLOCK_ALL,true;
		'twilight_story = 22;
		npctalk "You're too late. You idiots!";
		sleep2 2000;
		npctalk "They already escaped using the airships!";
		sleep2 2000;
		npctalk "Now, I, Sweety will take care of the idiots that just came in!";
		sleep2 2000;
		setpcblock PCBLOCK_ALL,false;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnBossSummon";
		end;
	}
	if ('twilight_story == 23) {
		specialeffect EF_NPC_STOP;
		npctalk "... Do ... n't ... you ... dare ... ";
	}
	end;


OnBossSummon:
	hideonnpc();
	monster instance_mapname("1@bamq"),125,39,"Sweety",20642,1,instance_npcname(strnpcinfo(0))+"::OnBossKill";
	setunitdata $@mobid[0],UMOB_HP,500000;
	end;

OnBossKill:
	'twilight_story = 23;
	hideoffnpc();
	enablenpc instance_npcname("Est#172_tgd_4");
	enablenpc instance_npcname("#tgd_bamq_exit");
	end;
}

1@bamq,108,41,5	script	Est#172_tgd_4	4_F_ESTLOVELOY,{
	cutin "ep162_est01",2;
	mes "[ Est ]";
	mes "It's a little late, but it's over.";
	mes "So that's you. The guy who was dragging his feet in the garden.";
	next;
	mes "[ Est ]";
	mes "Did this guy think that controlling the automatic dolls will help him buy time?";
	next;
	cutin "ep162_est02",2;
	mes "[ Est ]";
	mes "After all, they're just automatic dolls, right?";
	mes "I had no choice but to blow the automatic dolls of the mansion...";
	next;
	cutin "ep162_est01",2;
	mes "[ Est ]";
	mes "Let's get out of here.";
	mes "I'll be waiting at the guesthouse.";
	close3;
}

1@bamq,37,59,3	script	Almond#172_hst	20629,{
	if ('Sweety < 2) {
		cutin "ep172_beta",2;
		mes "[ Almond ]";
		mes "I can't understand.";
		mes "Why Sweety likes to push his body to the limit...";
		next;
		mes "[ Almond ]";
		mes "And it's my job to fix his destroyed body.";
		mes "Please deal with him enough to make him satisfied.";
		close3;
	}
	cutin "ep172_beta",2;
	mes "[ Almond ]";
	mes "I will take care of moving Sweety.";
	mes "Do you want to wrap up now?";
	next;
	if (select("Let's wrap up.:I'll explore the garden first.") == 2) {
		mes "[ Almond ]";
		mes "There is still some intruders out there.";
		mes "Are you aware of it?";
		close3;
	}
	mes "[ Almond ]";
	mes "Then, shall we?";
	close2;
	warp "ba_in01",18,255;
	end;
}

1@bamq,32,49,3	script	Sweety#172_hst	4_EP17_Sweety,{
	if ('Sweety == 0) {
		cutin "ep172_Sweety01",2;
		mes "[ Sweety ]";
		mes "Hey, are you ready?";
		mes "Come at me anytime.";
		mes "I'm sure I'll feel better once I beat you.";
		next;
		if (select("Wait a bit.:Let's go!") == 1) {
			mes "[ Sweety ]";
			mes "What, did you come here to explore the garden?";
			mes "The heart hunter don't know what happened to me, but...";
			next;
			cutin "ep172_Sweety02",2;
			mes "[ Sweety ]";
			mes "What, why?";
			mes "I'm not like them at all, I'm an excellent creature!";
			mes "So did the teachers.";
			mes "But, those guys are nothing but failures.";
			close3;

		}
		cutin "",255;
		npctalk "I won't lose to you this time!";
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummon";
		'Sweety = 1;
		close;
	}
	specialeffect EF_NPC_STOP;
	mes "He is not responding to anything.";
	mes "I think he fainted from my attacks.";
	npctalk "... ...";
	close;

OnSummon:
	hideonnpc();
	monster instance_mapname("1@bamq"),32,49,"Sweety",20642,1,instance_npcname(strnpcinfo(0))+"::OnBossKill";
	end;

OnBossKill:
	hideoffnpc();
	'Sweety = 2;
	end;
}

1@bamn,33,224,0	script	#tgd_bamn_to_bamnq	WARPNPC,1,1,{
	end;

OnTouch:
	if ('twilight_story == 21)
		warp instance_mapname("1@bamq"),103,39;
	end;
}

1@bamn,33,224,0	script	#tgd_bamn_to_bamnq2	WARPNPC,1,1,{
	end;

OnTouch:
	warp instance_mapname("1@bamq"),13,39;
	end;
}

1@bamq,101,39,0	script	#tgd_bamq_exit	WARPNPC,1,1,{
	end;

OnTouch:
	warp "ba_maison",33,220;
	end;
}

1@bamq,8,39,0	script	#tgd_bamq_exit2	WARPNPC,1,1,{
	end;

OnTouch:
	if (!'summon) {
		'summon = 1;
		donpcevent instance_npcname("#twilight_story_mob") + "::OnSummonDaily";
	}
	warp instance_mapname("1@bamn"),33,220;
	end;
}

1@bamn,150,47,3	script	#twilight_garden_beta-0	4_EP17_BROKENBETA,{
	if (isbegin_quest(18024) == 1) {
		.@id = atoi(replacestr(strnpcinfo(2),"twilight_garden_beta-",""));
		if (getd("'broken_beta_" + .@id) == 0) {
			mes "The body of Manager Beta is hidden under the landscapes.";
			next;
			if (rand(1,10) > 5) {
				mes "I've recovered the core.";
				getitem 1000226,1;
			} else
				mes "The core slot is empty.";
			mes "I'll let Alpha know later so she can retrieve the doll's body.";
			setd("'broken_beta_" + .@id,1);
		}
	}
	mes "The core part is empty.";
	end;

OnInstanceInit:
	questinfo QTYPE_CLICKME,QMARK_YELLOW,"isbegin_quest(18024) == 1";
	end;
}

1@bamn,208,87,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-1	4_EP17_BROKENBETA
1@bamn,221,193,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-2	4_EP17_BROKENBETA
1@bamn,314,146,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-3	4_EP17_BROKENBETA
1@bamn,338,267,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-4	4_EP17_BROKENBETA
1@bamn,275,313,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-5	4_EP17_BROKENBETA
1@bamn,207,276,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-6	4_EP17_BROKENBETA
1@bamn,131,197,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-7	4_EP17_BROKENBETA
1@bamn,64,187,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-8	4_EP17_BROKENBETA
1@bamn,70,256,3	duplicate(#twilight_garden_beta-0)	#twilight_garden_beta-9	4_EP17_BROKENBETA

1@bamn,1,1,0	script	#twilight_story_mob	-1,{
	end;

OnSpawn00:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill00";
	setarray .@xy,118,293,20681,124,294,20681,121,300,20681,119,308,20681,128,308,20681;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Heart Hunter",.@xy[.@i+2],1,.@event$;
	end;

OnMobKill00:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill00";
	if (!mobcount('map$,.@event$)) {
		disablenpc instance_npcname("Est#172_tgd_0");
		enablenpc instance_npcname("Est#172_tgd_1");
	}
	end;

OnSpawn01:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill01";
	setarray .@xy,210,282,20681,208,283,20681,210,285,20681,213,284,20681,213,281,20681;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Heart Hunter",.@xy[.@i+2],1,.@event$;
	end;

OnMobKill01:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill01";
	if (!mobcount('map$,.@event$)) {
		'twilight_story = 5;
		enablenpc instance_npcname("Est#172_tgd_2");
		enablenpc instance_npcname("Almond#172_tgd_0");
	}
	end;

OnSpawn02:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill02";
	setarray .@xy,246,208,20681,248,208,20681,248,206,20681,246,206,20681;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Heart Hunter",.@xy[.@i+2],1,.@event$;
	end;

OnMobKill02:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill02";
	if (!mobcount('map$,.@event$))
		'twilight_story = 11;
	end;

OnSpawn03:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill03";
	setarray .@xy,248,208,20699,248,206,20699;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Heart Hunter Commander",.@xy[.@i+2],1,.@event$;
	end;

OnMobKill03:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill03";
	if (!mobcount('map$,.@event$))
		'twilight_story = 12;
	end;

OnSummonDaily:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnDailyKill";
	setarray .@xy,20681,120,257,20681,130,255,20681,130,244,20681,127,184,20699,188,240,20699,203,248,20699,203,252,20681,301,234,20681,302,239,
	20681,297,244,20699,197,52,20681,120,83,20681,117,84,20681,137,176,20681,143,181,20699,62,165;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i+1],.@xy[.@i+2],"--ja--",.@xy[.@i],1,.@event$;
OnDailyKill:
	end;
}
