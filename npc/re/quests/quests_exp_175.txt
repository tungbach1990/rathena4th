//===== rAthena Script =======================================
//= Quests EXP
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- NPC quests related to new dungeons.
//===== Changelog: ===========================================
//= 1.0 Initial release [Capuche]
//============================================================


// mag_dun03
yuno_fild03,38,146,5	script	Lily Blanc	-1,{
	if (BaseLevel < 175) {
		mes "[Lily]";
		mes "...?";
		mes "...well, I don't think the guards will let you in...";
		next;
		mes "[Lily]";
		mes "If you are qualified to enter the 3rd floor of Nogg Road, would you accept a quest?";
		mes "We need a lot of work.";
		mes "The guards said level 175 is enough...";
		close;
	}
	mes "[Lily]";
	mes "Friendly adventurer, would you mind helping us with the research for our conference report?";
	mes "Only an adventurer's eye is the only way for us to live.";
	next;
	switch( select( "Listen to the request", "Suspicious...", "Reject" ) ) {
	case 1:
		mes "[Lily]";
		mes "Generous adventurer! Thank you.";
		mes "The request is on the 3rd floor of Nogg Road, a place I can't enter yet, but the conference wants a report...";
		next;
		setarray .@quest_list[1], 8550, 8553, 8556, 8559;
		.@s = select( "Deleter that caused evolution", "Extermination of nightmares that became reality", "Check density", "Small things crumbling", "Next time" );

		if (.@s < 5) {
			.@quest_first = .@quest_list[.@s];
			.@quest_delay = .@quest_first + 1;
			.@quest_daily = .@quest_first + 2;

			callsub( S_CheckDelay, .@quest_delay );

			if (isbegin_quest(.@quest_first) < 2)
				.@quest_ongoing = .@quest_first;
			else
				.@quest_ongoing = .@quest_daily;
		}

		switch( .@s ) {
		case 1:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "Our doctor said that monsters change their characteristics and appearance when they live in different places...";
					mes "Dr. and me too... Because combat isn't my major.";
					next;
					mes "[Lily]";
					mes "So here... Let's see.";
					mes "^0000FFThere is a report that the Rigid Earth Deleter and Rigid Sky Deleter^000000 caused abnormal evolution.";
					mes "But I can't even approach them because of their ferocious personality...";
					next;
				}
				mes "[Lily]";
				mes "Haunted on the 3rd floor of Nogg Road,";
				mes "^0000FFKill 15 Rigid Sky Deleter^000000";
				mes "and 15 ^0000FFRigid Earth Deleter^000000 and tell me how hard they were.";
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "Haunted on the 3rd floor of Nogg Road,";
				mes "^0000FFKill 15 Rigid Sky Deleter^000000";
				mes "and 15 ^0000FFRigid Earth Deleter^000000 and tell me how hard they were.";
				close;
			case 2:
				mes "[Lily]";
				mes "How was it?";
				mes "How was it different from just an air or ground Deleter?";
				next;
				mes "[Lily]";
				mes "Hmm... are you okay?";
				mes "Hmmmm. Okay";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 4852278,3396600 );
			}
			end;
		case 2:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "Rigid Nightmare Terror is a hardened nightmare terror...";
					mes "I've only seen Nightmare terror. Just looking at it made me stiff and unable to move. Although the professor dragged me out...";
					next;
					mes "[Lily]";
					mes "So here... Let's see.";
				}
				else {
					mes "[Lily]";
				}
				mes "Haunted on the 3rd floor of Nogg Road,";
				mes "^0000FFDefeat 10 Rigid Nightmare Terror^000000 and tell me how hard it is.";
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "Haunted on the 3rd floor of Nogg Road";
				mes "^0000FF Defeat 10 Rigid Nightmare Terror^000000 and tell me how hard it is.";
				close;
			case 2:
				mes "[Lily]";
				mes "How was it?";
				mes "How was it different from just a Nightmare Terror?";
				next;
				mes "[Lily]";
				mes "Huh... Really?";
				mes "What should I do... my God. Is it real?";
				mes "Hmmmm. Okay";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 1661868,1163304 );
			}
			end;
		case 3:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "I've heard that there is a huge difference in density between a Lava Golem and a Rigid Lava Golem.";
				}
				else {
					mes "[Lily]";
				}
				mes "Haunted on the 3rd floor of Nogg Road";
				mes "^0000FFDefeat 10 Rigid Lava Golem^000000 and tell me the density difference.";
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "Haunted on the 3rd floor of Nogg Road";
				mes "^0000FFDefeat 10 Rigid Lava Golem^000000 and tell me the density difference.";
				close;
			case 2:
				mes "[Lily]";
				mes "How was it?";
				mes "How was it different from just a Lava Golem?";
				next;
				mes "[Lily]";
				mes "Um... that's right. The shell...";
				mes "Yes? The soles of the feet? Indeed...";
				mes "Hmmmm. Okay";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 1590396,1113275 );
			}
			end;
		case 4:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "Rigid Explosion and Rigid Blazer are said to crumble much better than normal Explosion and Blazers.";
					mes "...our professor.";
					next;
					mes "[Lily]";
					mes "Haunted on the 3rd floor of Nogg Road,";
					mes "^0000FFKill 10 Rigid Explosion^000000";
					mes "^0000FFand 10 Rigid Blazer^000000 and tell them how they feel.";
					mes "You don't have to touch it. You can tell just by looking at it...";
				}
				else {
					mes "[Lily]";
					mes "Haunted on the 3rd floor of Nogg Road,";
					mes "^0000FFKill 10 Rigid Explosion^000000";
					mes "^0000FFand 10 Rigid Blazer^000000 and tell them how they feel.";
				}
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "Haunted on the 3rd floor of Nogg Road,";
				mes "^0000FFKill 10 Rigid Explosion^000000";
				mes "^0000FFand 10 Rigid Blazer^000000 and tell them how they feel.";
				close;
			case 2:
				mes "[Lily]";
				mes "How was it?";
				mes "How is it different from just Explosion or Blazer?";
				next;
				mes "[Lily]";
				mes "Um... that's right. Pass...";
				mes "It's much more crumbly...";
				mes "Aha, that's a lot more dust.";
				mes "Hmmmm. Okay";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 3251388,2275966 );
			}
			end;
		case 5:
			mes "[Lily]";
			mes "Ah...";
			mes "When will I ever be able to escape from the professor's magic...";
			close;
		}
		end;
	case 2:
		mes "[Lily]";
		mes "As you can see, I'm just an incompetent sage.";
		mes "The professor I'm serving is a bit stubborn... I mean, someone has to investigate.";
		next;
		mes "[Lily]";
		mes "But I am still not allowed to enter the 3rd floor of Nogg Road.";
		mes "How did I get to the entrance and they said they would never let me in.";
		mes "That's right... not a place for sage to fit in.";
		next;
		mes "[Lily]";
		mes "Then what can I do? Why don't all the bards sing only what they see and hear?";
		mes "Some people have heard of this and that, others have told me, so it must be.";
		next;
		mes "[Lily]";
		mes "Don't worry. I'll use the adventurer's name when I report it to the professor...";
		mes "Yes...  " + strcharinfo(0) + ".";
		mes "All right.";
		next;
		mes "[Lily]";
		mes "There is a list of monsters that will be research materials, so please check the quests if you are interested.";
		close;
	case 3:
		mes "[Lily]";
		mes "Ah, no...!";
		mes "Generous adventurer, please free me from the professor's magic...!";
		close;
	}
	end;

S_CheckDelay:
	switch( checkquest(getarg(0),PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		mes "[Lily]";
		mes "You have already completed today's report.";
		mes "Will you help me with tomorrow's report...?";
		close;
	case 2:
		erasequest getarg(0);
		break;
	}
	return;

S_Give:
	next;
	if (select( "I accept.", "I'm busy." ) == 2) {
		mes "[Lily]";
		mes "Originally, research is about persistence.";
		mes "Help me when you're not busy.";
		close;
	}
	mes "[Lily]";
	mes "Thank you!";
	mes "I'll be waiting here.";
	setquest getarg(0);
	close;

S_SetDelay:
	.@quest_ongoing = getarg(0);
	.@quest_first = getarg(1);
	.@quest_delay = getarg(2);
	next;
	mes "[Lily]";
	mes "Thank you for your kind explanation.";
	mes "But I don't think you can understand the whole story by just that. Although this is the beauty of requesting an investigation...";
	mes "I'll ask you again tomorrow.";
	if (.@quest_ongoing == .@quest_first)
		completequest .@quest_ongoing;
	else
		erasequest .@quest_ongoing;
	setquest .@quest_delay;
	getexp getarg(3), getarg(4);
	close;

OnInit:
	setunitdata getnpcid(0), UNPC_CLASS, JOB_SAGE;
	setunitdata getnpcid(0), UNPC_SEX, SEX_FEMALE;
	setunitdata getnpcid(0), UNPC_HAIRCOLOR, 6;
	setunitdata getnpcid(0), UNPC_HAIRSTYLE, 22;
	setunitdata getnpcid(0), UNPC_HEADTOP, 316;

	// completed first quest
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8550,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8553,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8556,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8559,HUNTING) == 2" );

	// completed daily
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8552,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8555,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8558,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8561,HUNTING) == 2" );

	// timeout
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8551,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8554,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8557,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8560,PLAYTIME) == 2" );

	// not started first quest
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8550) == 0 && checkquest(8551,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8553) == 0 && checkquest(8553,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8556) == 0 && checkquest(8555,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8559) == 0 && checkquest(8557,PLAYTIME) == -1" );

	// not started daily
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8550) == 2 && isbegin_quest(8552) == 0 && checkquest(8551,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8553) == 2 && isbegin_quest(8555) == 0 && checkquest(8554,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8556) == 2 && isbegin_quest(8558) == 0 && checkquest(8557,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8559) == 2 && isbegin_quest(8561) == 0 && checkquest(8560,PLAYTIME) == -1" );
	end;
}


// ein_dun03
einbech,143,244,3	script	Apprentice Researcher Sally#extdq01	4_F_LGTGIRL,{
	if (BaseLevel < 180) {
		mes "[Sally]";
		mes "Hello, adventurer!";
		mes "Please introduce me to a talented adventurer!";
		mes "A very, very important matter on which my job is at stake..!";
		next;
		mes "^4d4dffThere are quests over 180 Lv.^000000";
		close;
	}
	if (checkquest(7879,HUNTING) == 2) {
		mes "[Sally]";
		mes "We were able to collect enough data for Poisonous and Toxious!";
		erasequest 7879;
		setquest 7880;
		getexp 27512240,19258560;
		next;
	}
	if (checkquest(7881,HUNTING) == 2) {
		mes "[Sally]";
		mes "I have collected enough data for Green and Red Minerals!";
		erasequest 7881;
		setquest 7882;
		getexp 27836640,19485600;
		next;
	}
	if (checkquest(7883,HUNTING) == 2) {
		mes "[Sally]";
		mes "The data for White and Purple Minerals are as follows. It was very helpful.";
		erasequest 7883;
		setquest 7884;
		getexp 27838400,19486880;
		next;
	}
	if (checkquest(7885,HUNTING) == 2) {
		mes "[Sally]";
		mes "Abyssman's data is always amazing.";
		erasequest 7885;
		setquest 7886;
		getexp 6938800,4857160;
		next;
	}
	if (checkquest(7887,HUNTING) == 2) {
		mes "[Sally]";
		mes "You have successfully obtained Jewelry Ant's and White Porcellio's data!";
		erasequest 7887;
		setquest 7888;
		getexp 13854240,9697960;
		next;
	}
	mes "[Sally]";
	mes "When will this poor apprenticeship end?";
	next;
	mes "[Sally]";
	mes "I am desperately looking for help from a talented adventurer.";
	mes "Just like you!";
	mes "Would you like to help protect the poor trainee researcher's job?";
	next;
	switch( select( "New Toxic Gas Research", "Special Mineral Research 1", "Special Mineral Research 2", "Abyssman Research", "Mine Bug Research", "Why are you doing this?" ) ) {
	case 1:
		switch( checkquest(7880,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Um, don't overdo it.";
			mes "Opportunities always come.";
			close;
		case 2:
			erasequest 7880;
			break;
		}
		switch( checkquest(7879,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "I need data to research the poison gas monster newly discovered in the Einbech Mine Dungeon.";
			mes "It is difficult to deal with the monsters occupying the mines with the manpower here, so we have to deal with both.";
			next;
			mes "[Sally]";
			mes "I'm collecting data while the adventurer is fighting ^4d4dffPoisonous and Toxious^000000.";
			mes "Isn't it a good deal?";
			next;
			if (select( "Received a request", "Rejected" ) == 2) {
				mes "[Sally]";
				mes "It's ok.";
				mes "The world is wide and there are many adventurers.";
				close;
			}
			mes "[Sally]";
			mes "Then please take care of the ^4d4dffPoisonous and Toxious^000000 that appear on the ^4d4dff3rd floor of the Einbech Mine Dungeon^000000!";
			setquest 7879;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Poisonous and Toxious are here on the 3rd floor of the Einbech Mine Dungeon.";
			mes "Probably, it's hard to deal with ordinary people like me, so I'll have to hide and watch what you kill and collect data.";
			close;
		case 2:
			end;
		}
		end;
	case 2:
		switch( checkquest(7882,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Um, don't overdo it.";
			mes "Opportunities always come.";
			close;
		case 2:
			erasequest 7882;
			break;
		}
		switch( checkquest(7881,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "There are several types of Minerals. The Minerals we want to investigate this time are ^4d4dffGreen Mineral and Red Mineral^000000.";
			mes "Collect data necessary for research while controlling the number of individuals!";
			next;
			if (select( "Received a request", "Rejected" ) == 2) {
				mes "[Sally]";
				mes "It's ok.";
				mes "The world is wide and there are many adventurers.";
				close;
			}
			mes "[Sally]";
			mes "Then please take care of the ^4d4dffGreen and Red Minerals^000000 that appear on the ^4d4dff3rd floor of the Einbech mine dungeon^000000!";
			setquest 7881;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Green Minerals and Red Minerals are found here on the 3rd floor of the Einbech Mine Dungeon.";
			mes "Be careful, they are dangerous.";
			close;
		case 2:
			end;
		}
		end;
	case 3:
		switch( checkquest(7884,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Um, don't overdo it.";
			mes "Opportunities always come.";
			close;
		case 2:
			erasequest 7884;
			break;
		}
		switch( checkquest(7883,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "There are several types of Minerals. The Minerals we want to investigate this time are ^4d4dffWhite Minerals and Purple Minerals^000000.";
			mes "Collect data necessary for research while controlling the number of individuals!";
			next;
			if (select( "Received a request", "Rejected" ) == 2) {
				mes "[Sally]";
				mes "It's ok.";
				mes "The world is wide and there are many adventurers.";
				close;
			}
			mes "[Sally]";
			mes "Then please take care of the ^4d4dffWhite Mineral and Purple Mineral^000000 that appear on the ^4d4dff3rd floor of the Einbech Mine Dungeon^000000!";
			setquest 7883;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "White minerals and Purple minerals are found here on the 3rd floor of the Einbech Mine Dungeon.";
			mes "Be careful, they are dangerous.";
			close;
		case 2:
			end;
		}
		end;
	case 4:
		switch( checkquest(7886,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Um, don't overdo it.";
			mes "Opportunities always come.";
			close;
		case 2:
			erasequest 7886;
			break;
		}
		switch( checkquest(7885,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "If you look into an abyss, it is said that the abyss is eaten, not meowed.";
			mes "I think the popular name Abyssman is also like that.";
			mes "So we need data from ^4d4dffAbyssman^000000.";
			next;
			if (select( "Received a request", "Rejected" ) == 2) {
				mes "[Sally]";
				mes "It's ok.";
				mes "The world is wide and there are many adventurers.";
				close;
			}
			mes "[Sally]";
			mes "Then, please take care of the ^4d4dffAbyssmen^000000 who appear in the ^4d4dff3rd floor of the Einbech Mine Dungeon^000000!";
			setquest 7885;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "The Abyssman are here on the 3rd floor of the Einbech Mine Dungeon.";
			mes "Be careful, they are dangerous.";
			close;
		case 2:
			end;
		}
		end;
	case 5:
		switch( checkquest(7888,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "Um, don't overdo it.";
			mes "Opportunities always come.";
			close;
		case 2:
			erasequest 7888;
			break;
		}
		switch( checkquest(7887,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "Specialized insects live in dark places like dungeons.";
			mes "The data to be collected this time are ^4d4dffWhite Porcellio and Jewelry Ant^000000, also known as mineworms.";
			mes "Is it very different from the normal Porcellio?";
			next;
			if (select( "Received a request", "Rejected" ) == 2) {
				mes "[Sally]";
				mes "It's ok.";
				mes "The world is wide and there are many adventurers.";
				close;
			}
			mes "[Sally]";
			mes "Then, please take care of the ^4d4dffWhite Porcellio and Jewelry Ant^000000 who appear in the ^4d4dff3rd floor of the Einbech Mine Dungeon^000000!";
			setquest 7887;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "White Porcellio and Jewelry Ant are here on the 3rd floor of the Einbech Mine Dungeon.";
			mes "Be careful, they are dangerous.";
			close;
		case 2:
			end;
		}
		end;
	case 6:
		mes "[Sally]";
		mes "Um? That's to be a full-time employee!";
		mes "Not long ago, I joined Regenschrum as a trainee researcher, but this is what I was appointed for.";
		mes "It's about collecting data on mining monsters, so this personnel evaluation depends on it.";
		next;
		mes "[Sally]";
		mes "And there were constant complaints from the headquarters asking what to do with the mine monsters.";
		mes "While collecting data, it also moderates monster subjugation. Whoops, if that's the case, this evaluation is a win-win, a win-win!";
		close;
	}
	end;

OnInit:
	// completed
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7879,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7881,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7883,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7885,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7887,HUNTING) == 2" );

	// timeout
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7880,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7882,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7884,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7886,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7888,PLAYTIME) == 2" );

	// not started
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7879) == 0 && checkquest(7880,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7881) == 0 && checkquest(7882,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7883) == 0 && checkquest(7884,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7885) == 0 && checkquest(7886,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7887) == 0 && checkquest(7888,PLAYTIME) == -1" );
	end;
}

einbech,156,243,3	script	Need help#exsk	1_M_SMITH,{
	mes "[Olive]";
	mes "The orders keep pouring in, and everyone who works there is running away... It's a big deal, a big deal.";
	close;
}

einbech,154,244,5	script	Need help#exsk_	4_F_JOB_BLACKSMITH,{
	mes "[Atri]";
	mes "Of course, I'd rather learn the sword faster! It's only ten years since I've sharpened a pickaxe, so I'll learn it quickly.";
	close;
}

einbech,149,257,4	script	Site manager Elmen#extdq0	4_TOWER_16,{
	if (checkweight(25814,100) == 0 || checkweight(1201,1) == 0 || (MaxWeight - Weight) < 10000) {
		mes "- You cannot proceed because you have too many items. -";
		close;
	}
	mes "[Elmen]";
	mes "Hello. Adventurer.";
	mes "If you're here to purchase Dynite mining equipment or refine ore into Dynite, you've come to the right place.";
	next;
	switch( select( "Dynite Refinement", "Dynite Shop", "Dynite Equipment Exchange" ) ) {
	case 1:
		npctalk "Elmen: You can refine up to 100 Dynite at a time. The machine is small.", "", bc_self;
		mes "[Elmen]";
		mes "<ITEM>[Dynite]<INFO>25814</INFO></ITEM> Refined Ore.";
		mes "<ITEM>[Radium]<INFO>25809</INFO></ITEM> ^777777(currently " + countitem(25809) + ")^000000";
		mes "<ITEM>[Lindium]<INFO>25810</INFO></ITEM> ^777777(currently " + countitem(25810) + ")^000000";
		mes "<ITEM>[Odium]<INFO>25811</INFO></ITEM> ^777777(currently " + countitem(25811) + ")^000000";
		mes "<ITEM>[Ferdium]<INFO>25812</INFO></ITEM> ^777777(currently " + countitem(25812) + ")^000000";
		mes "<ITEM>[Fidium]<INFO>25813</INFO></ITEM> ^777777(currently " + countitem(25813) + ")^000000";
		mes "You need one each of the five types of ore.";
		next;
		input .@val;
		if (.@val > 100) {
			mes "[Elmen]";
			mes "I'm sorry. Adventurer.";
			mes "Dynite Refinement can only be refined up to 100 at a time? The machine is small, so there is no help. Please understand.";
			close;
		}
		if (.@val == 0) {
			mes "[Elmen]";
			mes "Cancel?";
			mes "If you need refinement of the Dynite, please find it anytime. There is no one who can do it without me.";
			close;
		}
		if (countitem(25809) < .@val || countitem(25810) < .@val || countitem(25811) < .@val || countitem(25812) < .@val || countitem(25813) < .@val) {
			mes "[Elmen]";
			mes "Hey! This makes it difficult for each other, don't you run out of materials needed for refining? The materials!";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		progressbar_npc "FFFF00",1;
		delitem 25809,.@val;	// Ein_REDIUM
		delitem 25810,.@val;	// Ein_RINDIUM
		delitem 25811,.@val;	// Ein_ODIUM
		delitem 25812,.@val;	// Ein_PURDIUM
		delitem 25813,.@val;	// Ein_WHIDIUM
		getitem 25814,.@val;	// Ein_DYNITE
		mes "[Elmen]";
		mes "" + .@val + " Dynite have been refined. It's perfect today!";
		close;
	case 2:
		mes "[Elmen]";
		mes "There are various accessories and modification permits. Look carefully at what you want before buying.";
		close2;
		callshop "barter_ein_dun03";
		end;
	case 3:
		mes "[Elmen]";
		mes "I can exchange the equipment with the Dynite, but it's already in a tightly sealed box.";
		mes "Give me 50 ^4d4dffDynites^000000 and I will open a ^4d4dffa weapon box^000000 for you.";
		next;
		mes "[Elmen]";
		mes "I don't know what will come out, but the quality is good. Would you like to open it?";
		next;
		if (select( "Close your eyes and exchange.", "Do not exchange." ) == 2) {
			mes "[Elmen]";
			mes "Kee, this is a waste, it was a good deal.";
			close;
		}
		if (countitem(25814) < 50) {
			mes "[Elmen]";
			mes "You don't have enough Dynite friends.";
			mes "I don't know what's going to come out, so it's hard to go anywhere and get this kind of equipment at this price.";
			close;
		}
		// unknown text
		getgroupitem(IG_Ein_1HITEMbox);
		end;
	}
}